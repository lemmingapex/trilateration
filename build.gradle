/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'scott' at '10/12/15 12:35 PM' with Gradle 2.7
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/2.7/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'com.lemmingapex.trilateration'
//archivesBaseName 'Trilateration'
version '1.0.2'

def remotePublish = false

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required {remotePublish}
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (!remotePublish) {
                repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository')
            } else {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'trilateration'
                    packaging 'jar'
                    description 'Solves a formulation of n-D space trilateration problem using a nonlinear least squares optimizer'
                    url 'https://github.com/lemmingapex/trilateration'

                    scm {
                        connection 'scm:git:git@github.com:lemmingapex/trilateration.git'
                        developerConnection 'scm:git:git@github.com:lemmingapex/trilateration.git'
                        url 'git@github.com:lemmingapex/trilateration.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://raw.githubusercontent.com/lemmingapex/trilateration/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'lemmingapex'
                            name 'Scott Wiedemann'
                            email 'lemmingapex@gmail.com'
                            roles {
                                role 'developer'
                            }
                            timezone 'UTC-07'
                        }
                    }
                }
            }
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

task wrapper(type: Wrapper) {
   gradleVersion = '2.14'
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.apache.commons:commons-math3:3.6.1'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}
